1 REM v1.0.0a
10 POKE STATUS 2,Y: CLEAR : DIM S$(0)*26:Q=INT ((STATUS 3-STATUS 2)/36)-1: DIM Q$(Q)*34:Y=PEEK STATUS 2
20 " " IF N=0 AND Q=0 THEN 10
30 LOCK : CLS : PAUSE "  PC-2 POCKET ORGANIZER": PAUSE "  Copr. 1983 Tandy Corp.": ON ERROR GOTO 5E3
40 IF Y<82 OR Y>99 GOSUB "IYR":Y=A
100 WAIT 0: CLS : PRINT " Queue    Wait    Set"
110 GOSUB "INK": ON Z-16 GOTO 1E3,110,3E3,110,4E3
120 IF Z=24 UNLOCK : END
130 GOTO 110
1000 CLS : WAIT 0: PRINT "  Add Lkp Lst Rvw Pur Ctrl"
1010 GOSUB "INK": IF Z>16 AND Z<23 GOTO 100*(Z-6)
1020 IF Z=24 THEN 100
1030 GOTO 1010
1100 CLS : IF N=Q WAIT : PRINT "Queue full": GOTO 1E3
1110 A=Y: INPUT "Event year?  19";A: IF A<82 OR A>99 THEN 1110
1120 GOSUB "IDS": GOSUB "ITI"
1130 F=255:R$="": INPUT "Repeat code? ";R$: GOSUB "R$": IF F<1 OR F>255 THEN 1130
1140 G=3: INPUT "Beeps? (1-255) ";G: IF G<1 OR G>255 THEN 1140
1150 Q$(0)="": INPUT "Text? ";Q$(0)
1160 Q$(0)=CHR$ A+CHR$ B+CHR$ C+CHR$ D+CHR$ E+CHR$ F+CHR$ G+"1"+Q$(0)
1170 GOSUB "ADD":P$="Add another": GOSUB "YN": GOTO 1E3+100*(Z$="Y")
1200 WAIT : IF N=0 PRINT "Queue empty": GOTO 1E3
1210 GOSUB "QPR": INPUT "Target text? ";Q$
1220 FOR I=1 TO N: FOR J=9 TO LEN Q$(I)-LEN Q$+1
1230 IF MID$ (Q$(I),J,LEN Q$)=Q$ GOSUB "DSP":J=32
1240 NEXT J: NEXT I: GOTO 1E3
1300 IF N=0 THEN 1200
1310 CLS : WAIT 0: PRINT "Start ";: GOSUB "IYR"
1320 CLS : PRINT "Start ";: GOSUB "IDS":S$=CHR$ A+CHR$ B+CHR$ C
1330 CLS : PRINT "End ";: GOSUB "IYR"
1340 CLS : PRINT "End ";: GOSUB "IDS":E$=CHR$ A+CHR$ B+CHR$ C: GOSUB "QPR"
1350 FOR I=1 TO N: IF Q$(I)>=S$ AND LEFT$ (Q$(I),3)<=E$ GOSUB "DSP"
1360 NEXT I: GOTO 1E3
1400 IF N=0 THEN 1200
1410 I=1:V=0
1420 W=0: CLS : PRINT STR$ I;": ";: WAIT 0: IF V PRINT MID$ (Q$(I),9,26): GOTO 1440
1430 GOSUB "DED": PRINT S$(0)
1440 GOSUB "KEY": IF Z=9 LET V=1-V: GOTO 1420
1441 IF Z=10 AND I<N LET I=I+1: GOTO 1420
1442 IF Z=11 AND I>1 LET I=I-1: GOTO 1420
1443 IF Z=24 THEN 1E3
1444 IF Z=42 GOSUB "DEL":I=I-(I>N): GOTO 1400+20*(N>0)
1445 IF Z=69 THEN 1700
1446 IF Z=80 TEXT : CSIZE 1:W=1: GOSUB "DSP":W=0
1450 GOTO 1440
1500 P$="Are you SURE": GOSUB "YN": IF Z$="N" THEN 1E3
1510 CLS : GOSUB "IYR": GOSUB "IDS":D$=CHR$ A+CHR$ B+CHR$ C: PRINT "Purging queue"
1520 J=0: FOR I=1 TO N: IF Q$(I)>=D$ LET J=I:I=N
1530 NEXT I: IF J=1 THEN 1E3
1540 FOR I=J TO N:Q$(I-J+1)=Q$(I): NEXT I:N=N-J+1: GOTO 1E3
1600 WAIT 0: CLS : PRINT "Save Load Clear"
1610 GOSUB "INK": ON Z-16 GOSUB 1630,1640,1660
1620 GOTO 1600-600*(Z=24)
1630 GOSUB "IFN": PRINT #F$;N,Q$(*): RETURN 
1640 IF N>0 LET P$="Save old data": GOSUB "YN": IF A$="Y" GOSUB 1630
1650 GOSUB "IFN": INPUT #F$;N,Q$(*): RETURN 
1660 P$="Are you SURE": GOSUB "YN": IF Z$="Y" LET N=0
1670 RETURN 
1700 GOSUB "DED": WAIT 0:D=D+1:E=E+1
1710 CLS : PRINT "Year 19";STR$ A;: INPUT "? ";A: GOTO 1710
1720 IF A<82 OR A>99 THEN 1710
1730 CLS : PRINT "Date";B;"/";STR$ C;: INPUT "? ";Z$: GOSUB 11010: GOTO 1730
1740 CLS : PRINT "Time";D-1;":";CHR$ (48*(E<10));STR$ (E-1);: INPUT "? ";Z$: GOSUB 12010: GOTO 1740
1750 CLS : PRINT "Repeat code ";R$;: INPUT "? ";R$: GOSUB "R$": GOTO 1750
1760 F=F+255*(F=0): IF F<1 OR F>255 THEN 1750
1770 CLS : PRINT STR$ G;: INPUT " beeps? ";G: GOTO 1770
1780 Q$(0)=MID$ (Q$(I),9,26)
1790 CLS : PRINT Q$(0);: INPUT "? ";Q$(0): GOTO 1790
1800 Q$(0)=CHR$ A+CHR$ B+CHR$ C+CHR$ D+CHR$ E+CHR$ F+CHR$ G+"1"+Q$(0): CLS 
1810 PRINT "Updating queue": GOSUB "DEL": GOSUB "ADD": GOTO 1420
3000 WAIT 0:T$=CHR$ (48*(TIME <1E5))+STR$ TIME :B=VAL LEFT$ (T$,2):C=VAL MID$ (T$,3,2):A=Y
3010 D=VAL MID$ (T$,5,2):E=VAL MID$ (T$,8,2):U=1: GOSUB 18020: CLS : PRINT S$(0);" ";N;" items"
3020 Z$=CHR$ Y+CHR$ B+CHR$ C+CHR$ (D+1)+CHR$ (E+1): FOR I=1 TO N:Y$=MID$ (Q$(I),8,1)
3030 IF (LEFT$ (Q$(I),5)>Z$ OR Y$<>"1") AND (Y$<>"2" OR E/10<>INT (E/10)) THEN 3140
3040 X$="2": WAIT 0: PRINT MID$ (Q$(I),9,26): BEEP ASC MID$ (Q$(I),7,1): FOR J=1 TO 250
3050 Z=ASC INKEY$ : IF Z=13 OR Z=42 CLS :J=250:X$="0": IF Z=42 GOSUB "DEL": NEXT J: GOTO 3140
3060 NEXT J:Q$(I)=LEFT$ (Q$(I),7)+X$+MID$ (Q$(I),9,26): GOSUB "DED":D=D+1:E=E+1
3070 IF VAL R$=0 THEN 3140
3080 @(5-K)=@(5-K)+VAL R$
3090 IF E>60 LET E=E-60:D=D+1: GOTO 3090
3100 IF D>24 LET D=D-24:C=C+1: GOTO 3100
3110 GOSUB "DIM": IF C>X LET C=C-X:B=B+1: GOTO 3110
3120 IF B>12 LET B=B-12:A=A+1: GOTO 3120
3130 Q$(0)=CHR$ A+CHR$ B+CHR$ C+CHR$ D+CHR$ E+CHR$ F+CHR$ G+"1"+MID$ (Q$(I),9,26): GOSUB "DEL": GOSUB "ADD"
3140 IF ASC INKEY$ =24 LET I=N: NEXT I: GOTO 100
3150 NEXT I: GOTO 3E3
4000 CLS : GOSUB "IYR":Y=A
4010 GOSUB "IDS": GOSUB "ITI": TIME =B*1E4+C*100+D-1+(E-1)/100: GOTO 100
5000 A=STATUS 4:B=PEEK &789B: CLS : WAIT : IF B=43 PRINT "TAPE ERROR": GOTO 100
5010 PRINT "*** ERROR";B;" in";A;" ***": GOTO 100
10000 "INK" IF LEN INKEY$  THEN 1E4
10010 "KEY"Z=ASC INKEY$ : IF Z=0 THEN 10010
10020 BEEP ON : RETURN 
11000 "IDS"Z$="": INPUT "Date? (mm/dd) ";Z$
11010 B=VAL Z$:C=VAL MID$ (Z$,3+(MID$ (Z$,2,1)<>"/"),2): GOSUB "DIM"
11020 IF C<1 OR C>X OR B<1 OR B>12 CLS : GOTO 11E3
11030 RETURN 
12000 "ITI"Z$="": INPUT "Time? (hh.mm) ";Z$
12010 D=1+INT VAL Z$:E=1+VAL MID$ (Z$,3+(MID$ (Z$,2,1)<>"."),2)
12020 IF E<1 OR E>60 OR D<1 OR D>24 CLS : GOTO 12E3
12030 RETURN 
13000 "ADD" IF N=0 LET N=1:Q$(1)=Q$(0): RETURN 
13010 K=0: FOR L=1 TO N: IF Q$(L)>Q$(0) LET K=L:L=N
13020 NEXT L: IF K>0 FOR L=N TO K STEP -1:Q$(L+1)=Q$(L): NEXT L:Q$(K)=Q$(0):N=N+1: RETURN 
13030 N=N+1:Q$(N)=Q$(0): RETURN 
14000 "DEL" FOR L=I TO N-1:Q$(L)=Q$(L+1): NEXT L:N=N-1: RETURN 
15000 "DSP"Q$(0)=MID$ (Q$(I),9,26): GOSUB "DED"
15010 IF W LPRINT S$(0): LPRINT TAB 2;Q$(0): LF 1: RETURN 
15020 WAIT : PRINT S$(0): PRINT Q$(0): RETURN 
16000 "QPR"Z$="": WAIT 0: CLS 
16010 PRINT CHR$ 34;"P";CHR$ 34;: INPUT " to print, ENTER ";Z$
16020 W=(Z$="P"): CLS : IF W TEXT : CSIZE 1
16030 RETURN 
17000 "IFN" CLS :F$="": INPUT "File name? ";F$
17010 INPUT "Ready tape, press ENTER";Z$
17020 BEEP OFF : RETURN 
18000 "DED" FOR K=1 TO 7:@(K)=ASC MID$ (Q$(I),K,1): NEXT K:D=D-1:E=E-1: IF F=255 LET F=0
18010 K=INT (F/60):R$=STR$ (F-60*K): IF INT K>0 AND VAL R$>0 LET R$=R$+MID$ ("HDM",K,1)
18020 S$(0)=STR$ B+"/"+STR$ C+"/"+STR$ A+"  "+STR$ D+":"+CHR$ (48*(E<10))+STR$ E
18030 IF U=0 LET S$(0)=S$(0)+"  "+R$+" b"+STR$ G
18040 U=0: RETURN 
19000 "YN"Z$="": CLS : WAIT 0: PRINT P$;: INPUT "? (Y/N) ";Z$
19010 IF Z$="Y" OR Z$="N" RETURN 
19020 GOTO 19E3
20000 "DIM"X=31-3*(B=2)-(B=4 OR B=6 OR B=9 OR B=11)+(B=2 AND A/4=INT (A/4)): RETURN 
21000 "R$"Z$=RIGHT$ (R$,1):F=INT VAL R$: IF F<0 OR F>59 LET F=0: RETURN 
21010 F=F+60*((Z$="H")+2*(Z$="D")+3*(Z$="M")):F=F+255*(F=0): RETURN 
22000 "IYR"A=Y: INPUT "Year?  19";A
22010 IF A<82 OR A>99 CLS : GOTO 22E3
22020 RETURN 
50000 END
